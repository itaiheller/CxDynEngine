description = 'Manages Dynamic Engines for Checkmarx SAST solution'

test {
    // sets jasypt key for unit tests
    //  if you want specify a different key add -Djasypt.encryptor.password=<key> on gradle command line,
    //  e.g. ./gradlew build -Djasypt.encryptor.password=changeme
    systemProperty "jasypt.encryptor.password", System.getProperty("jasypt.encryptor.password") ?: "CxR0cks!!"
}

bootRun {
    // required for passing JVM system properties via the gradle command line
    systemProperties = System.properties
}

jar {
    manifest {
        attributes("Implementation-Title": "Checkmarx Dynamic Engine Manager",
                   "Implementation-Version": version)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task zip(type: Zip) {
    dependsOn bootRepackage
    from 'src/main/resources/dist'
    from jar
    into 'cx-dyn-engines'
}

task dist(type: Copy) {
    dependsOn zip
    from 'build/distributions'
    from 'build/libs'
    into '../archive/'
    include '*-sources.jar'
    include '*.zip'
    rename { String fileName ->
        fileName.replace('.jar', '.zip')
    }
    rename { String fileName ->
        fileName.replace('-SNAPSHOT', '')
    }
}
